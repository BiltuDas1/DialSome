cmake_minimum_required(VERSION 3.16)
project(DialSome VERSION 0.1 LANGUAGES CXX)

# Extra Metadatas
add_definitions(-DGITHUB_REPO="https://github.com/BiltuDas1/DialSome")
add_definitions(-DGITHUB_CONFIGURATIONS="http://192.168.31.129:8080/settings.ini")

find_package(Qt6 REQUIRED COMPONENTS
    Quick
    QuickControls2
    Network
    WebSockets
)

qt_standard_project_setup(REQUIRES 6.8)

# OpenSSL
if (ANDROID)
    include(${CMAKE_CURRENT_SOURCE_DIR}/libraries/android_openssl/android_openssl.cmake)
else()
    find_package(OpenSSL REQUIRED)
endif()

qt_add_executable(DialSome
    main.cpp
)

if (ANDROID)
    add_android_openssl_libraries(DialSome)
endif()

qt_add_qml_module(DialSome
    URI DialSome
    VERSION 1.0
    QML_FILES
        Main.qml
        ui/LoginPage.qml
        ui/MainPage.qml
    SOURCES
        src/lib/securestorage.h src/lib/securestorage.cpp
        src/lib/google.h src/lib/google.cpp
        src/utils.h src/utils.cpp
        src/fcmmanager.h src/fcmmanager.cpp
        src/backend.h src/backend.cpp
)

qt_add_resources(DialSome "configs"
    PREFIX "/"
    FILES qtquickcontrols2.conf
)

target_link_libraries(DialSome PRIVATE
    Qt6::Quick
    Qt6::QuickControls2
    Qt6::Network
    Qt6::WebSockets
)

target_include_directories(DialSome PRIVATE src src/lib)

if(ANDROID)
    set_property(TARGET DialSome PROPERTY QT_ANDROID_PACKAGE_SOURCE_DIR
        ${CMAKE_CURRENT_SOURCE_DIR}/android)
endif()

if(ANDROID AND NOT CMAKE_BUILD_TYPE STREQUAL "Debug")
    set_target_properties(DialSome PROPERTIES INTERPROCEDURAL_OPTIMIZATION_RELEASE ON)
    
    # This forces the linker to strip all debug symbols
    target_link_options(DialSome PRIVATE "-Wl,--gc-sections" "-Wl,--strip-all")
endif()

if(NOT CMAKE_BUILD_TYPE STREQUAL "Debug")
    add_definitions(-DQT_NO_DEBUG_OUTPUT)
    set(QT_QML_DEBUG OFF)
endif()